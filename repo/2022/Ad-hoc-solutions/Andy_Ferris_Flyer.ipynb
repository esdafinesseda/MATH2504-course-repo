{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using BenchmarkTools\n",
    "using Plots\n",
    "\n",
    "function sieve_of_Eratosthenes(n)\n",
    "    primebits = ones(Bool,n) # Will contain true if the index is prime (initially all assumed prime)\n",
    "    primebits[1] = false # The number 1 is not prime\n",
    "    p = 2 # Smallest prime\n",
    "    @inbounds while p ≤ n\n",
    "        i = 2p\n",
    "        while i ≤ n  # \\le +[TAB]\n",
    "            primebits[i] = false\n",
    "            i += p\n",
    "        end\n",
    "        p += 1\n",
    "        while p ≤ n && !primebits[p]\n",
    "            p += 1\n",
    "        end\n",
    "    end\n",
    "    return primebits\n",
    "end\n",
    "\n",
    "function check_Goldbachs_slow(n)\n",
    "    primebits = sieve_of_Eratosthenes(n)\n",
    "    primes = (1:n)[primebits]\n",
    "    out = zeros(Int, n)\n",
    "\n",
    "    for i in 2:2:n\n",
    "        for p in primes\n",
    "            if in(i - p, primes)\n",
    "                out[i] += 1\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return out[2:2:n]\n",
    "end\n",
    "\n",
    "function check_Goldbachs(n::Integer)\n",
    "    # Below we are working in the subset of the numbers\n",
    "    # The primes we care about are odd numbers in the range 3:2:n\n",
    "    # The output are even numbers in the range 2:2:n\n",
    "\n",
    "    n_half = n >> 0x01\n",
    "    primebits = sieve_of_Eratosthenes(n)\n",
    "    @inbounds mask = primebits[3:2:n]\n",
    "    @inbounds primes = (Int32(1):Int32(n_half - 1))[mask] # not really the primes!\n",
    "\n",
    "    out = zeros(UInt16, n_half)\n",
    "    out[2] = 0x0001 # 4 is 2 + 2, and below we only deal with odd primes\n",
    "    @inbounds for x in primes\n",
    "        i_out = x + 2\n",
    "        for i_mask in 1:(n_half - x)\n",
    "            out[i_out] += mask[i_mask]\n",
    "            i_out += 1\n",
    "        end\n",
    "    end\n",
    "    return out\n",
    "end\n",
    "\n",
    "# Test the implementation\n",
    "@assert check_Goldbachs_slow(100) == check_Goldbachs(100)\n",
    "\n",
    "n = 1_000_000\n",
    "checks = @btime check_Goldbachs(n)\n",
    "\n",
    "scatter(2:2:n, checks, legend=false, xlabel=\"n\", ylabel=\"Number of Goldbach pairs\", markersize=0.1)\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.3",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
